import { Array, arrayCreate, arrayGet, arraySet } from "@array";
import { assertEqual, assertEqualString } from "@assert";
import { List, listFromArray } from "@list";
import {
  String,
  stringFromNumber,
  stringSplitBy,
  stringToArray,
  stringToLines,
  stringToNumber,
} from "@string";
import { getValue: getInput } from "./input.txt";

data Row = {
  conditions: List<i32>,
  damaged: List<i32>,
  damageTotal: i32,
}

let parseRow = (line: String): Row -> {
  let parts = stringSplitBy(line, ' ');
  let conditions = [];

  for char in listFromArray(stringToArray(parts.get(0))) {
    conditions.push(char);
  }

  let damaged = [];
  let damagedParts = stringSplitBy(parts.get(1), ',');
  let damageTotal = 0;

  for damagedPart in damagedParts {
    let damage = stringToNumber(damagedPart);

    damaged.push(damage);

    damageTotal = (damageTotal + damage);
  }

  Row.New({ conditions: conditions, damaged: damaged, damageTotal: damageTotal })
}

let parseInput = (input: String): List<Row> -> {
  let lines = stringToLines(input.trim());
  let rows = [];

  for line in lines {
    rows.push(parseRow(line));
  }

  rows
}

let cacheSpacing = (): i32 -> 30

let createCache = (): Array<i32> -> {
  let cache = arrayCreate((cacheSpacing() * (cacheSpacing() * cacheSpacing())));

  for _, i in listFromArray(cache) {
    arraySet(cache, i, --(1));
  }

  cache
}

let placeDamage = (conditions: List<i32>, offset: i32, width: i32): List<i32> -> {
  let newConditions = conditions.clone();

  for condition, index in conditions {
    if (condition == '?') {
      newConditions.set(index, '.');
    }

    if (index >= offset) {
      newConditions.set(index, '#');
    }

    if (index >= (offset + width)) {
      newConditions.set(index, conditions.get(index));
    }
  }

  newConditions
}

let conditionsContainDot = (conditions: List<i32>): boolean -> {
  let result = false;

  for condition in conditions {
    if (condition == '.') {
      result = true;
    }
  }

  result
}

let isValidPlacement = (conditions: List<i32>, maxDamage: i32, offset: i32, width: i32): boolean -> {
  let prevIndex = (offset - 1);
  let nextIndex = (offset + width);

  case {
    ((offset + width) > conditions.size()) -> false;
    conditionsContainDot(conditions.slice(offset, (offset + width))) -> false;
    ((prevIndex >= 0) && (conditions.get(prevIndex) == '#')) -> false;
    ((nextIndex < (conditions.size() - 1)) && (conditions.get(nextIndex) == '#')) -> false;
    _ -> {
      let damageTotal = 0;

      for condition2 in placeDamage(conditions, offset, width) {
        if (condition2 == '#') {
          damageTotal = (damageTotal + 1);
        }
      }

      (damageTotal <= maxDamage)
    };
  }
}

let countArrangementsRec = (cache: Array<i32>, row: Row, offset: i32, damagedIndex: i32): i32 -> {
  let damageTotal = 0;

  for condition2 in row.conditions {
    if (condition2 == '#') {
      damageTotal = (damageTotal + 1);
    }
  }

  let cacheKey = (damagedIndex + ((cacheSpacing() * offset) + (damageTotal * (cacheSpacing() * cacheSpacing()))));
  let cached = arrayGet(cache, cacheKey);

  case {
    (cached >= 0) -> cached;
    (offset >= (row.conditions).size()) -> 0;
    _ -> {
      let arrangements = 0;
      let damageWidth = (row.damaged).get(damagedIndex);
      let valid = isValidPlacement(row.conditions, row.damageTotal, offset, damageWidth);

      if valid {
        let nextConditions = placeDamage(row.conditions, offset, damageWidth);
        let nextDamagedIndex = (damagedIndex + 1);
        let nextOffset = (offset + (damageWidth + 1));

        if (nextDamagedIndex < (row.damaged).size()) {
          let newRow = Row.New({
            conditions: nextConditions,
            damaged: row.damaged,
            damageTotal: row.damageTotal,
          });

          arrangements = (arrangements + countArrangementsRec(cache, newRow, nextOffset, nextDamagedIndex));
        } else {
          arrangements = (arrangements + 1);
        }
      }

      arrangements = (arrangements + countArrangementsRec(cache, row, (offset + 1), damagedIndex));

      arraySet(cache, cacheKey, arrangements);

      arrangements
    };
  }
}

let countArrangements = (row: Row): i32 -> {
  countArrangementsRec(createCache(), row, 0, 0)
}

let unfoldRow = (row: Row): Row -> {
  Row.New({
    conditions: [
      ...row.conditions,
      '?',
      ...row.conditions,
      '?',
      ...row.conditions,
      '?',
      ...row.conditions,
      '?',
      ...row.conditions,
    ],
    damaged: [
      ...row.damaged,
      ...row.damaged,
      ...row.damaged,
      ...row.damaged,
      ...row.damaged,
    ],
    damageTotal: (row.damageTotal * 5),
  })
}

let unfoldRows = (rows: List<Row>): List<Row> -> {
  let newRows = [];

  for row in rows {
    newRows.push(unfoldRow(row));
  }

  newRows
}

let testCountArrangements = (): unit -> {
  assertEqual(countArrangements(parseRow("? 1")), 1);
  assertEqual(countArrangements(parseRow("?? 1")), 2);
  assertEqual(countArrangements(parseRow("??? 1")), 3);
  assertEqual(countArrangements(parseRow("??? 2")), 2);
  assertEqual(countArrangements(parseRow("??? 1,1")), 1);
  assertEqual(countArrangements(parseRow("??? 3")), 1);
  assertEqual(countArrangements(parseRow("??? 4")), 0);
  assertEqual(countArrangements(parseRow("#?? 1")), 1);
  assertEqual(countArrangements(parseRow("#?? 2")), 1);
  assertEqual(countArrangements(parseRow("#.. 2")), 0);
  assertEqual(countArrangements(parseRow("### 2")), 0);
  assertEqual(countArrangements(parseRow(".## 2")), 1);
  assertEqual(countArrangements(parseRow("?????#???? 2,1,1")), 11);
}

let solvePart1 = (input: String): String -> {
  let rows = parseInput(input);
  let result = 0;

  for row in rows {
    result = (result + countArrangements(row));
  }

  stringFromNumber(result)
}

let _solvePart2 = (input: String): String -> {
  let rows = unfoldRows(parseInput(input));
  let result = 0;

  for row in rows {
    result = (result + countArrangements(row));
  }

  stringFromNumber(result)
}

testCountArrangements();

let input = getInput();
let part1 = solvePart1(input);

assertEqualString(part1, "7204");

# todo
# let part2 = (solvePart2 input)
# (assertEqualString part2 "???")
