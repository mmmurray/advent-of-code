data Rating = {
  x: i32,
  m: i32,
  a: i32,
  s: i32,
}

data Condition = {
  property: i32,
  operator: i32,
  value: i32,
}

data WorkflowStep =
  | WorkflowStep {
      target: String,
    }
  | WorkflowStepConditional {
      target: String,
      condition: Condition,
    }

data Workflow = {
  name: String,
  steps: List<WorkflowStep>,
}

data System = {
  ratings: List<Rating>,
  workflows: Map<Workflow>,
}

let parseWorkflow = (input: String): Workflow -> {
  let workflowParts = (stringSplitBy input '{');
  let name = (listGet workflowParts 0);
  let innerPart = (listGet workflowParts 1);
  let inner = (stringSlice innerPart 0 (- (stringSize innerPart) 1));
  let steps = [];
  let stepParts = (stringSplitBy inner ',');

  for stepPart in stepParts {
    if (stringContains stepPart ":") {
      let conditionParts = (stringSplitBy stepPart ':');
      let conditionPart = (listGet conditionParts 0);
      let target = (listGet conditionParts 1);
      let step = WorkflowStepConditional {
        target: target,
        condition: Condition {
          property: (stringGet conditionPart 0),
          operator: (stringGet conditionPart 1),
          value: (stringToNumber (stringSlice conditionPart 2 (stringSize conditionPart))),
        },
      };

      (listPush steps step);
    } else {
      let step2 = WorkflowStep { target: stepPart };

      (listPush steps step2);
    }
  }

  return Workflow { name: name, steps: steps };
}

let parseRating = (input: String): Rating -> {
  let rating = Rating { x: 0, m: 0, a: 0, s: 0 };
  let inner = (stringSlice input 1 (- (stringSize input) 1));
  let ratingParts = (stringSplitBy inner ',');

  for ratingPart in ratingParts {
    let propertyParts = (stringSplitBy ratingPart '=');
    let propertyName = (listGet propertyParts 0);
    let propertyValue = (stringToNumber (listGet propertyParts 1));

    if (stringEquals propertyName "x") {
      rating.x = propertyValue;
    }

    if (stringEquals propertyName "m") {
      rating.m = propertyValue;
    }

    if (stringEquals propertyName "a") {
      rating.a = propertyValue;
    }

    if (stringEquals propertyName "s") {
      rating.s = propertyValue;
    }
  }

  return rating;
}

let parseInput = (input: String): System -> {
  let lines = (stringToLines (stringTrim input));
  let inWorkflowsSection = true;
  let workflows = (mapCreate<Workflow>);
  let ratings = [];

  for line in lines {
    if (== (stringSize line) 0) {
      inWorkflowsSection = false;
    } else {
      if inWorkflowsSection {
        let workflow = (parseWorkflow line);

        (mapSet workflows workflow.name workflow);
      } else {
        (listPush ratings (parseRating line));
      }
    }
  }

  return System { workflows: workflows, ratings: ratings };
}

let printSystem = (system: System): unit -> {
  let out = (stringBuilderCreate 1024);
  let workflows = system.workflows;

  for workflowItem in (mapEntries workflows) {
    let workflow = workflowItem.value;

    (stringBuilderAppend out workflow.name);
    (stringBuilderAppend out "{");

    for step, stepIndex in workflow.steps {
      if (> stepIndex 0) {
        (stringBuilderAppend out ",");
      }

      let m = match step {
        WorkflowStep { target } -> {
          (stringBuilderAppend out target);
        };
        WorkflowStepConditional { target, condition } -> {
          (stringBuilderAppend out (charToString condition.property));
          (stringBuilderAppend out (charToString condition.operator));
          (stringBuilderAppend out (stringFromNumber condition.value));
          (stringBuilderAppend out ":");
          (stringBuilderAppend out target);
        };
      };
    }

    (stringBuilderAppend out "}\n");
  }

  (stringBuilderAppend out "\n");

  for rating in system.ratings {
    (stringBuilderAppend out "{x=");
    (stringBuilderAppend out (stringFromNumber rating.x));
    (stringBuilderAppend out ",m=");
    (stringBuilderAppend out (stringFromNumber rating.m));
    (stringBuilderAppend out ",a=");
    (stringBuilderAppend out (stringFromNumber rating.a));
    (stringBuilderAppend out ",s=");
    (stringBuilderAppend out (stringFromNumber rating.s));
    (stringBuilderAppend out "}\n");
  }

  (print (stringBuilderBuild out));
}

let doesConditionApply = (condition: Condition, rating: Rating): boolean -> {
  let propertyValue = 0;

  if (== condition.property 'x') {
    propertyValue = rating.x;
  }

  if (== condition.property 'm') {
    propertyValue = rating.m;
  }

  if (== condition.property 'a') {
    propertyValue = rating.a;
  }

  if (== condition.property 's') {
    propertyValue = rating.s;
  }

  if (== condition.operator '>') {
    return (> propertyValue condition.value);
  }

  return (< propertyValue condition.value);
}

let isPartAccepted = (workflows: Map<Workflow>, rating: Rating): boolean -> {
  let currentWorkflow = "in";

  while true {
    # (print (stringConcat "currentWorkflow: " currentWorkflow))

    if (stringEquals currentWorkflow "A") {
      return true;
    }

    if (stringEquals currentWorkflow "R") {
      return false;
    }

    let workflow = (optionExpect (mapGet workflows currentWorkflow));

    for step in workflow.steps {
      let accepted = false;
      let rejected = false;
      let continue = match step {
        WorkflowStep { target } -> {
          currentWorkflow = target;

          return false;
        };
        WorkflowStepConditional { target, condition } -> {
          let result = true;

          if (doesConditionApply condition rating) {
            currentWorkflow = target;
            result = false;
          }

          return result;
        };
      };

      if (!continue) {
        break;
      }
    }
  }

  return false;
}

let solvePart1 = (input: String): String -> {
  let system = (parseInput input);
  let result = 0;

  for rating in system.ratings {
    if (isPartAccepted system.workflows rating) {
      let sum = (+ rating.x (+ rating.m (+ rating.a rating.s)));

      result = (+ result sum);
    }
  }

  return (stringFromNumber result);
}

let main = (): unit -> {
  let input = (fileRead "input.txt");
  let part1 = (solvePart1 input);

  (print "Part 1:");
  (print part1);
  (assertEqualString part1 "287054");

  # let part2 = (solvePart2 input)
  # (print "Part 2:")
  # (print part2)
  # (assertEqualString part2 "???")
}
