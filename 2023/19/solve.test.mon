import { assertEqualString } from "@assert";
import { print } from "@io";
import { _type_List, listGet, listPush } from "@list";
import { _type_Map, mapCreate, mapGet, mapSet } from "@map";
import { _type_Option } from "@option";
import {
  _type_String,
  charToString,
  stringBuilderAppend,
  stringBuilderBuild,
  stringBuilderCreate,
  stringContains,
  stringEquals,
  stringFromNumber,
  stringGet,
  stringSize,
  stringSlice,
  stringSplitBy,
  stringToLines,
  stringToNumber,
  stringTrim,
} from "@string";
import { getValue: getInput } from "./input.txt";

let optionExpect = <T>(option: Option<T>): T -> {
  match option {
    Some { value } -> value;
    None -> (@T 0);
  }
}

data Rating = {
  x: i32,
  m: i32,
  a: i32,
  s: i32,
}

data Condition = {
  property: i32,
  operator: i32,
  value: i32,
}

data WorkflowStep =
  | WorkflowStep {
      target: String,
    }
  | WorkflowStepConditional {
      target: String,
      condition: Condition,
    }

data Workflow = {
  name: String,
  steps: List<WorkflowStep>,
}

data System = {
  ratings: List<Rating>,
  workflows: Map<String, Workflow>,
}

let parseWorkflow = (input: String): Workflow -> {
  let workflowParts = stringSplitBy(input, '{');
  let name = listGet(workflowParts, 0);
  let innerPart = listGet(workflowParts, 1);
  let inner = stringSlice(innerPart, 0, (stringSize(innerPart) - 1));
  let steps = [];
  let stepParts = stringSplitBy(inner, ',');

  for stepPart in stepParts {
    if stringContains(stepPart, ":") {
      let conditionParts = stringSplitBy(stepPart, ':');
      let conditionPart = listGet(conditionParts, 0);
      let target = listGet(conditionParts, 1);
      let step = WorkflowStepConditional({
        target: target,
        condition: Condition({
          property: stringGet(conditionPart, 0),
          operator: stringGet(conditionPart, 1),
          value: stringToNumber(stringSlice(conditionPart, 2, stringSize(conditionPart))),
        }),
      });

      listPush(steps, step);
    } else {
      let step2 = WorkflowStep({ target: stepPart });

      listPush(steps, step2);
    }
  }

  Workflow({ name: name, steps: steps })
}

let parseRating = (input: String): Rating -> {
  let rating = Rating({ x: 0, m: 0, a: 0, s: 0 });
  let inner = stringSlice(input, 1, (stringSize(input) - 1));
  let ratingParts = stringSplitBy(inner, ',');

  for ratingPart in ratingParts {
    let propertyParts = stringSplitBy(ratingPart, '=');
    let propertyName = listGet(propertyParts, 0);
    let propertyValue = stringToNumber(listGet(propertyParts, 1));

    if stringEquals(propertyName, "x") {
      rating.x = propertyValue;
    }

    if stringEquals(propertyName, "m") {
      rating.m = propertyValue;
    }

    if stringEquals(propertyName, "a") {
      rating.a = propertyValue;
    }

    if stringEquals(propertyName, "s") {
      rating.s = propertyValue;
    }
  }

  rating
}

let parseInput = (input: String): System -> {
  let lines = stringToLines(stringTrim(input));
  let inWorkflowsSection = true;
  let workflows = mapCreate();
  let ratings = [];

  for line in lines {
    if (stringSize(line) == 0) {
      inWorkflowsSection = false;
    } else {
      if inWorkflowsSection {
        let workflow = parseWorkflow(line);

        mapSet(workflows, workflow.name, workflow);
      } else {
        listPush(ratings, parseRating(line));
      }
    }
  }

  System({ workflows: workflows, ratings: ratings })
}

let _printSystem = (system: System): unit -> {
  let out = stringBuilderCreate(1024);
  let workflows = system.workflows;

  for workflowItem in workflows.entries() {
    let workflow = workflowItem.value;

    stringBuilderAppend(out, workflow.name);
    stringBuilderAppend(out, "{");

    for step, stepIndex in workflow.steps {
      if (stepIndex > 0) {
        stringBuilderAppend(out, ",");
      }

      let _ = match step {
        WorkflowStep { target } -> {
          stringBuilderAppend(out, target);
        };
        WorkflowStepConditional { target, condition } -> {
          stringBuilderAppend(out, charToString(condition.property));
          stringBuilderAppend(out, charToString(condition.operator));
          stringBuilderAppend(out, stringFromNumber(condition.value));
          stringBuilderAppend(out, ":");
          stringBuilderAppend(out, target);
        };
      };
    }

    stringBuilderAppend(out, "}\n");
  }

  stringBuilderAppend(out, "\n");

  for rating in system.ratings {
    stringBuilderAppend(out, "{x=");
    stringBuilderAppend(out, stringFromNumber(rating.x));
    stringBuilderAppend(out, ",m=");
    stringBuilderAppend(out, stringFromNumber(rating.m));
    stringBuilderAppend(out, ",a=");
    stringBuilderAppend(out, stringFromNumber(rating.a));
    stringBuilderAppend(out, ",s=");
    stringBuilderAppend(out, stringFromNumber(rating.s));
    stringBuilderAppend(out, "}\n");
  }

  print(stringBuilderBuild(out));
}

let doesConditionApply = (condition: Condition, rating: Rating): boolean -> {
  let propertyValue = case {
    (condition.property == 'x') -> rating.x;
    (condition.property == 'm') -> rating.m;
    (condition.property == 'a') -> rating.a;
    _ -> rating.s;
  };

  case {
    (condition.operator == '>') -> (propertyValue > condition.value);
    _ -> (propertyValue < condition.value);
  }
}

let isPartAccepted = (workflows: Map<String, Workflow>, rating: Rating): boolean -> {
  let currentWorkflow = "in";
  let result = false;
  let loop = true;

  while loop {
    # (print (stringConcat "currentWorkflow: " currentWorkflow))

    loop = case {
      stringEquals(currentWorkflow, "A") -> {
        result = true;

        false
      };
      stringEquals(currentWorkflow, "R") -> {
        result = false;

        false
      };
      _ -> {
        let workflow = optionExpect(mapGet(workflows, currentWorkflow));
        let loop = true;

        for step in workflow.steps {
          if loop {
            let continue = match step {
              WorkflowStep { target } -> {
                currentWorkflow = target;

                false
              };
              WorkflowStepConditional { target, condition } -> {
                let inner = true;

                if doesConditionApply(condition, rating) {
                  currentWorkflow = target;
                  inner = false;
                }

                inner
              };
            };

            if !(continue) {
              loop = false;
            }
          }
        }

        true
      };
    };
  }

  result
}

let solvePart1 = (input: String): String -> {
  let system = parseInput(input);
  let result = 0;

  for rating in system.ratings {
    if isPartAccepted(system.workflows, rating) {
      let sum = (rating.x + (rating.m + (rating.a + rating.s)));

      result = (result + sum);
    }
  }

  stringFromNumber(result)
}

let input = getInput();
let part1 = solvePart1(input);

assertEqualString(part1, "287054");

# todo
# let part2 = (solvePart2 input)
# (assertEqualString part2 "???")
