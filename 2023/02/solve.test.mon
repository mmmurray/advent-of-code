import { assertEqualString } from "@assert";
import { _type_List, listGet, listPush } from "@list";
import { max } from "@math";
import {
  _type_String,
  stringEquals,
  stringFromNumber,
  stringSize,
  stringSlice,
  stringSplitBy,
  stringToLines,
  stringToNumber,
  stringTrim,
} from "@string";
import { getValue: getInput } from "./input.txt";

data Selection = {
  red: i32,
  blue: i32,
  green: i32,
}

data Game = {
  id: i32,
  selections: List<Selection>,
}

let parseSelection = (input: String): Selection -> {
  let valueParts = stringSplitBy(input, ',');
  let selection = _type_Selection.New({ red: 0, blue: 0, green: 0 });

  for valuePart in valueParts {
    let pairParts = stringSplitBy(stringTrim(valuePart), ' ');
    let value = stringToNumber(listGet(pairParts, 0));
    let color = listGet(pairParts, 1);

    if stringEquals(color, "red") {
      selection.red = value;
    }

    if stringEquals(color, "green") {
      selection.green = value;
    }

    if stringEquals(color, "blue") {
      selection.blue = value;
    }
  }

  selection
}

let parseInputLine = (line: String): Game -> {
  let parts = stringSplitBy(line, ':');
  let gameIdPart = listGet(parts, 0);
  let idPart = stringSlice(gameIdPart, 5, stringSize(gameIdPart));
  let id = stringToNumber(idPart);
  let selections = [];
  let selectionsPart = stringTrim(listGet(parts, 1));
  let selectionParts = stringSplitBy(selectionsPart, ';');
  let selectionIndex = 0;

  for selectionPart in selectionParts {
    let selection = parseSelection(selectionPart);

    listPush(selections, selection);

    selectionIndex = (selectionIndex + 1);
  }

  _type_Game.New({ id: id, selections: selections })
}

let parseInput = (input: String): List<Game> -> {
  let lines = stringToLines(input);
  let games = [];

  for line in lines {
    let game = parseInputLine(line);

    listPush(games, game);
  }

  games
}

let solvePart1 = (input: String): String -> {
  let games = parseInput(input);
  let result = 0;

  for game in games {
    let possible = true;

    for selection in game.selections {
      if ((selection.red > 12) || ((selection.green > 13) || (selection.blue > 14))) {
        possible = false;
      }
    }

    if possible {
      result = (result + game.id);
    }
  }

  stringFromNumber(result)
}

let solvePart2 = (input: String): String -> {
  let games = parseInput(input);
  let result = 0;

  for game in games {
    let minSelection = _type_Selection.New({ red: 0, green: 0, blue: 0 });

    for selection in game.selections {
      minSelection.red = max(minSelection.red, selection.red);
      minSelection.green = max(minSelection.green, selection.green);
      minSelection.blue = max(minSelection.blue, selection.blue);
    }

    let power = (minSelection.red * (minSelection.blue * minSelection.green));

    result = (result + power);
  }

  stringFromNumber(result)
}

let input = stringTrim(getInput());
let part1 = solvePart1(input);

assertEqualString(part1, "1853");

let part2 = solvePart2(input);

assertEqualString(part2, "72706");
