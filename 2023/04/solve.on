data Game = {
  winning: List<u64>,
  numbers: List<u64>,
}

let parseNumberList = (input: String): List<u64> -> {
  let parts = (stringSplitBy (stringTrim input) ' ')
  let numbers = (listCreateDefault<u64>)

  for part in parts {
    let trimmed = (stringTrim part)
    if (> (stringSize trimmed) 0) {
      let number = (stringToNumberU64 trimmed)
      (listPush numbers number)
    }
  }

  return numbers
}

let parseInput = (input: String): List<Game> -> {
  let lines = (stringToLines input)
  let games = (listCreateDefault<Game>)
  
  for line in lines {
    let lineParts = (stringSplitBy line ':')
    let sectionParts = (stringSplitBy (listGet lineParts 1) '|')
    let game = Game {
      winning: (parseNumberList (listGet sectionParts 0)),
      numbers: (parseNumberList (listGet sectionParts 1)),
    }
    (listPush games game)
  }

  return games
}

let solvePart1 = (input: String): String -> {
  let result = 0
  let games = (parseInput input)

  for game, gameIndex in games {
    let score = 0

    for number in game.numbers {
      for needed in game.winning {
        if (== number needed) {
          score = (max 1 (* score 2))
        }
      }
    }

    result = (+ result score)
  }

  return (stringFromNumber result)
}

let solvePart2 = (input: String): String -> {
  let result = 0
  let games = (parseInput input)

  let resultByGame = (arrayCreate<u64> (listSize games))
  let countByGame = (arrayCreate<u64> (listSize games))

  for game, gameIndex in games {
    let gameResult = 0u64

    for number in game.numbers {
      for needed in game.winning {
        if (== number needed) {
          gameResult = (+ gameResult 1)
        }
      }
    }

    (arraySet resultByGame gameIndex gameResult)
    (arraySet countByGame gameIndex 1u64)
  }

  for count, gameIndex in (listFromArray countByGame) {
    result = (+ result count)

    let gameStart = (+ (@u64 gameIndex) 1)
    let gameEnd = (+ gameStart (arrayGet resultByGame gameIndex))
    while (< gameStart gameEnd) {
      let currentCount = (arrayGet countByGame gameStart)
      let newCount = (+ currentCount (@u64 count))
      (arraySet countByGame gameStart newCount)
      gameStart = (+ gameStart 1)
    }
  }

  return (stringFromNumberU64 result)
}

let main = (): unit -> {
  let input = (stringTrim (fileRead "input.txt"))

  let part1 = (solvePart1 input)
  (print "Part 1:")
  (print part1)
  (assertEqualString part1 "21959")

  let part2 = (solvePart2 input)
  (print "Part 2:")
  (print part2)
  (assertEqualString part2 "5132675")
}
