import { assertEqualString } from "@assert";
import { _type_List, listGet, listPush, listSize } from "@list";
import { stringFromNumber, stringSplitBy, stringToLines, stringToNumber, stringTrim } from "@string";
import { _type_String } from "@string-base";
import { getValue: getInput } from "./input.txt";

let parseInput = (input: String): List<List<i32> > -> {
  let lines = stringToLines(stringTrim(input));
  let sequences = [];

  for line in lines {
    let sequence = [];
    let parts = stringSplitBy(line, ' ');

    for part in parts {
      let value = stringToNumber(part);

      listPush(sequence, value);
    }

    listPush(sequences, sequence);
  }

  return sequences;
}

let findDeltas = (sequence: List<i32>): List<i32> -> {
  let deltas = [];

  for b, index in sequence {
    if (index > 0) {
      let a = listGet(sequence, (index - 1));
      let delta = (b - a);

      listPush(deltas, delta);
    }
  }

  return deltas;
}

let isSequenceAllZero = (sequence: List<i32>): boolean -> {
  let allZero = true;

  for value in sequence {
    if (value != 0) {
      allZero = false;
    }
  }

  return allZero;
}

let findNextValue = (sequence: List<i32>): i32 -> {
  return case {
    isSequenceAllZero(sequence) -> 0;
    _ -> {
      let lastValue = listGet(sequence, (listSize(sequence) - 1));

      return (lastValue + findNextValue(findDeltas(sequence)));
    };
  };
}

let findPreviousValue = (sequence: List<i32>): i32 -> {
  return case {
    isSequenceAllZero(sequence) -> 0;
    _ -> {
      let firstValue = listGet(sequence, 0);

      return (firstValue - findPreviousValue(findDeltas(sequence)));
    };
  };
}

let solvePart1 = (input: String): String -> {
  let sequences = parseInput(input);
  let result = 0;

  for sequence in sequences {
    let nextValue = findNextValue(sequence);

    result = (result + nextValue);
  }

  return stringFromNumber(result);
}

let solvePart2 = (input: String): String -> {
  let sequences = parseInput(input);
  let result = 0;

  for sequence in sequences {
    let previousValue = findPreviousValue(sequence);

    result = (result + previousValue);
  }

  return stringFromNumber(result);
}

let main = (): unit -> {
  let input = getInput();
  let part1 = solvePart1(input);

  assertEqualString(part1, "1877825184");

  let part2 = solvePart2(input);

  assertEqualString(part2, "1108");
}
