import { assertEqualString } from "@assert";
import { _type_List, listFromString, listGet, listPush, listSet, listSize } from "@list";
import { _type_Map, mapCreate, mapEntries, mapGet, mapSet, mapSize } from "@map";
import { optionValue } from "@option";
import {
  charIsNumeric,
  stringFromNumber,
  stringSplitBy,
  stringToLines,
  stringToNumber,
  stringTrim,
} from "@string";
import { _type_String } from "@string-base";
import { getValue: getInput } from "./input.txt";

data Hand = {
  cards: List<i32>,
  bid: i32,
  score: i32,
}

let isFiveOfAKind = (countByCard: Map<i32>): boolean -> {
  let result = false;

  for item1 in mapEntries(countByCard) {
    if (item1.value == 5) {
      result = true;
    }
  }

  return result;
}

let isFourOfAKind = (countByCard: Map<i32>): boolean -> {
  let result = false;

  for item1 in mapEntries(countByCard) {
    if (item1.value == 4) {
      result = true;
    }
  }

  return result;
}

let isThreeOfAKind = (countByCard: Map<i32>): boolean -> {
  let result = false;

  for item1 in mapEntries(countByCard) {
    if (item1.value == 3) {
      result = true;
    }
  }

  return result;
}

let isFullHouse = (countByCard: Map<i32>): boolean -> {
  return (mapSize(countByCard) == 2);
}

let isTwoPair = (countByCard: Map<i32>): boolean -> {
  return (mapSize(countByCard) == 3);
}

let isOnePair = (countByCard: Map<i32>): boolean -> {
  return (mapSize(countByCard) == 4);
}

let scoreCards = (cards: List<i32>): i32 -> {
  let countByCard = mapCreate();
  let jokers = 0;

  for card in cards {
    if (card == 1) {
      jokers = (jokers + 1);
    } else {
      let key = stringFromNumber(card);
      let current = optionValue(0, mapGet(countByCard, key));

      mapSet(countByCard, key, (current + 1));
    }
  }

  let maxCount = 0;
  let maxCard = "2";

  for item0 in mapEntries(countByCard) {
    if (item0.value > maxCount) {
      maxCount = item0.value;
      maxCard = item0.key;
    }
  }

  mapSet(countByCard, maxCard, (maxCount + jokers));

  return case {
    isFiveOfAKind(countByCard) -> 7;
    isFourOfAKind(countByCard) -> 6;
    isFullHouse(countByCard) -> 5;
    isThreeOfAKind(countByCard) -> 4;
    isTwoPair(countByCard) -> 3;
    isOnePair(countByCard) -> 2;
    true -> 1;
  };

  return 1;
}

let parseInput = (input: String, jokers: boolean): List<Hand> -> {
  let hands = [];
  let lines = stringToLines(stringTrim(input));

  for line in lines {
    let parts = stringSplitBy(line, ' ');
    let cardsPart = listGet(parts, 0);
    let bidPart = listGet(parts, 1);
    let cards = [];

    for cardChar in listFromString(cardsPart) {
      let card = 0;

      if (cardChar == 'A') {
        card = 14;
      }

      if (cardChar == 'K') {
        card = 13;
      }

      if (cardChar == 'Q') {
        card = 12;
      }

      if (cardChar == 'J') {
        if jokers {
          card = 1;
        } else {
          card = 11;
        }
      }

      if (cardChar == 'T') {
        card = 10;
      }

      if charIsNumeric(cardChar) {
        card = (cardChar - '0');
      }

      listPush(cards, card);
    }

    let hand = Hand({ cards: cards, bid: stringToNumber(bidPart), score: scoreCards(cards) });

    listPush(hands, hand);
  }

  return hands;
}

let compareHands = (a: Hand, b: Hand): boolean -> {
  return case {
    (a.score == b.score) -> {
      let result = false;
      let loop = true;

      for cardA, cardIndex in a.cards {
        if loop {
          let cardB = listGet(b.cards, cardIndex);

          if (cardA != cardB) {
            result = (cardA > cardB);
            loop = false;
          }
        }
      }

      return result;
    };
    true -> (a.score > b.score);
  };
}

let sortHands = (hands: List<Hand>): List<Hand> -> {
  let endIndex = (listSize(hands) - 1);

  while (endIndex > 0) {
    let index = 0;

    while (index < endIndex) {
      let a = listGet(hands, index);
      let b = listGet(hands, (index + 1));

      if compareHands(a, b) {
        listSet(hands, index, b);
        listSet(hands, (index + 1), a);
      }

      index = (index + 1);
    }

    endIndex = (endIndex - 1);
  }

  return hands;
}

let scoreHands = (hands: List<Hand>): i32 -> {
  let result = 0;

  for hand, handIndex in hands {
    let rank = (handIndex + 1);

    result = (result + (rank * hand.bid));
  }

  return result;
}

let solvePart1 = (input: String): String -> {
  let hands = sortHands(parseInput(input, false));

  return stringFromNumber(scoreHands(hands));
}

let solvePart2 = (input: String): String -> {
  let hands = sortHands(parseInput(input, true));

  return stringFromNumber(scoreHands(hands));
}

let main = (): unit -> {
  let input = getInput();
  let part1 = solvePart1(input);

  assertEqualString(part1, "245794640");

  let part2 = solvePart2(input);

  assertEqualString(part2, "247899149");
}
