import { assert-equal } from "@assert";
import { list } from "@list";
import { string } from "@string";
import { get-value: get-example } from "./example.txt";
import { get-value: get-input } from "./input.txt";

data equation = { inputs: list<i64>, target: i64 }

let parse-input = (input: string): list<equation> -> {
  input.trim().to-lines().map((line: string) -> {
    let sections = line.split-by(": ");
    let target = sections.get(0).to-i64();
    let inputs = sections.get(1).split-by(" ").map((part: string) -> part.to-i64());

    equation.new({ target, inputs })
  })
}

let get-operator-permutations = (operators: list<i32>, size: i32): list<list<i32>> -> {
  case {
    size == 0 -> [[]];
    _ -> {
      let sub-permutations = get-operator-permutations(operators, size - 1);

      operators.flat-map((op: i32) -> sub-permutations.map((p: list<i32>) -> [...p, op]))
    };
  }
}

let do-operators-satisfy-equation = (e: equation, operators: list<i32>): boolean -> {
  let total = operators.reduce-indexed(e.inputs.get(0), (acc: i64, op: i32, index: i32) -> {
    let input = e.inputs.get(index + 1);

    case {
      op ==  '+'  -> acc + input;
      op ==  '*'  -> acc * input;
      op ==  '|'  -> "\(acc)\(input)".to-i64();
      _ -> acc;
    }
  });

  total == e.target
}

let is-equation-possible = (operators: list<i32>, e: equation): boolean -> {
  let operator-permutations = get-operator-permutations(operators, e.inputs.size() - 1);

  operator-permutations.some((operators: list<i32>) -> do-operators-satisfy-equation(e, operators))
}

let solve-common = (input: string, operators: list<i32>): i64 -> {
  parse-input(input).reduce(0, (acc: i64, e: equation) -> {
    case {
      is-equation-possible(operators, e) -> acc + e.target;
      _ -> acc;
    }
  })
}

let solve-part-1 = (input: string): i64 -> {
  solve-common(input, [ '+' ,  '*' ])
}

let solve-part-2 = (input: string): i64 -> {
  solve-common(input, [ '+' ,  '*' ,  '|' ])
}

assert-equal(solve-part-1(get-example()), 3749);
assert-equal(solve-part-1(get-input()), 5540634308362);
assert-equal(solve-part-2(get-example()), 11387);

# assert-equal(solve-part-2(get-input()), 472290821152397);
