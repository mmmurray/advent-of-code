import { assert-equal } from "@assert";
import { string } from "@string";
import { get-value: get-input } from "./input.txt";

let parse-input = (input: string): [[i32]] -> {
  input.trim().split-by-lines().map((line: string) -> {
    line.split-by-spaces().map((part: string) -> part.to-i32())
  })
}

let is-report-safe = (report: [i32]): boolean -> {
  let deltas = report.reduce-indexed([], (acc: [i32], level: i32, index: i32) -> {
    case {
      index > 0 -> [...acc, level - report.get(index - 1)];
      _ -> acc;
    }
  });
  let safe-increasing = deltas.filter((level: i32) -> level >= 1 && level <= 3);
  let safe-decreasing = deltas.filter((level: i32) -> level >= -3 && level <= -1);

  safe-increasing.size() == deltas.size() || safe-decreasing.size() == deltas.size()
}

let is-report-safe-tolerant = (report: [i32]): boolean -> {
  let sub-reports = [0..report.size()].map((index: i32) -> [...report].remove(index));

  is-report-safe(report) || sub-reports.some(is-report-safe)
}

let solve-part-1 = (input: string): string -> {
  let reports = parse-input(input);
  let safe-count = reports.filter(is-report-safe).size();

  safe-count.to-string()
}

let solve-part-2 = (input: string): string -> {
  let reports = parse-input(input);
  let safe-count = reports.filter(is-report-safe-tolerant).size();

  safe-count.to-string()
}

assert-equal(solve-part-1(get-input()), "598");
assert-equal(solve-part-2(get-input()), "634");
