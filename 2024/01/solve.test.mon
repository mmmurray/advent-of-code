import { assert-equal } from "@assert";
import { pair, pair-create } from "@pair";
import { string } from "@string";
import { get-value: get-input } from "./input.txt";

let parse-input = (input: string): pair<[i32], [i32]> -> {
  input.trim().to-lines().map((line: string) -> {
    let parts = line.split-whitespace().map((part: string) -> part.to-i32());

    pair-create(parts.get(0), parts.get(1))
  }).unzip()
}

let solve-part-1 = (input: string): string -> {
  let lists = parse-input(input);
  let pairs = lists.a.sort().zip(lists.b.sort());
  let result = pairs.reduce(0, (acc: i32, p: pair<i32, i32>) -> {
    acc + (p.a - p.b).abs()
  });

  result.to-string()
}

let solve-part-2 = (input: string): string -> {
  let lists = parse-input(input);
  let occurrences = lists.b.reduce([:], (acc: [i32:i32], b: i32) -> {
    let count = acc.get(b).or(0);

    acc.set(b, count + 1)
  });
  let result = lists.a.reduce(0, (acc: i32, a: i32) -> {
    let count = occurrences.get(a).or(0);

    acc + a * count
  });

  result.to-string()
}

assert-equal(solve-part-1(get-input()), "1970720");
assert-equal(solve-part-2(get-input()), "17191599");
