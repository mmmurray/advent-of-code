import { assert-equal } from "@assert";
import { list } from "@list";
import { map, map-create } from "@map";
import { abs } from "@math";
import { pair, pair-create } from "@pair";
import { string, string-from-number } from "@string";
import { get-value: get-input } from "./input.txt";

let parse-input = (input: string): pair<list<i32>, list<i32>> -> {
  input.trim().to-lines().map((line: string) -> {
    let parts = line.split-by("   ").map((part: string) -> part.to-i32());

    pair-create(parts.get(0), parts.get(1))
  }).unzip()
}

let solve-part-1 = (input: string): string -> {
  let lists = parse-input(input);
  let as = lists.a.sort();
  let bs = lists.b.sort();
  let result = as.reduce-indexed(0, (acc: i32, a: i32, index: i32) -> {
    let b = bs.get(index);

    acc + abs(a - b)
  });

  string-from-number(result)
}

let solve-part-2 = (input: string): string -> {
  let lists = parse-input(input);
  let occurrences = lists.b.reduce(map-create(), (acc: map<i32, i32>, b: i32) -> {
    let count = acc.get(b).or(0);

    acc.set(b, count + 1)
  });
  let result = lists.a.reduce(0, (acc: i32, a: i32) -> {
    let count = occurrences.get(a).or(0);

    acc + a * count
  });

  string-from-number(result)
}

let part-1 = solve-part-1(get-input());

assert-equal(part-1, "1970720");

let part-2 = solve-part-2(get-input());

assert-equal(part-2, "17191599");
