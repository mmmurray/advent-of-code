import { assert-equal } from "@assert";
import { grid, grid-compass-4, grid-from-string } from "@grid";
import { set, set-create } from "@set";
import { string } from "@string";
import { vec2, vec2-create } from "@vec2";
import { get-value: get-example } from "./example.txt";
import { get-value: get-input } from "./input.txt";

let parse-input = (input: string): grid<i32, i32> -> {
  grid-from-string(input.trim()).map((cell: i32, x: i32, y: i32) -> cell -  '0' )
}

let get-trailhead-score = (acc: set<vec2<i32>>, map: grid<i32, i32>, position: vec2<i32>, height: i32): set<vec2<i32>> -> {
  case {
    height == 9 -> acc.add(position);
    _ -> grid-compass-4().reduce(acc, (acc: set<vec2<i32>>, direction: vec2<i32>) -> {
      let next-height = height + 1;
      let next-position = position.add(direction);
      let candidate-height = map.get-safe(next-position.x, next-position.y).or(100);

      case {
        candidate-height == next-height -> get-trailhead-score(acc, map, next-position, next-height);
        _ -> acc;
      }
    });
  }
}

let get-trailhead-rating = (map: grid<i32, i32>, position: vec2<i32>, height: i32): i32 -> {
  case {
    height == 9 -> 1;
    _ -> grid-compass-4().reduce(0, (acc: i32, direction: vec2<i32>) -> {
      let next-height = height + 1;
      let next-position = position.add(direction);
      let candidate-height = map.get-safe(next-position.x, next-position.y).or(100);

      acc + case {
        candidate-height == next-height -> get-trailhead-rating(map, next-position, next-height);
        _ -> 0;
      }
    });
  }
}

let solve-part-1 = (input: string): i32 -> {
  let map = parse-input(input);

  map.reduce(0, (acc: i32, height: i32, x: i32, y: i32) -> {
    acc + case {
      height == 0 -> get-trailhead-score(set-create(), map, vec2-create(x, y), height).size();
      _ -> 0;
    }
  })
}

let solve-part-2 = (input: string): i32 -> {
  let map = parse-input(input);

  map.reduce(0, (acc: i32, height: i32, x: i32, y: i32) -> {
    acc + case {
      height == 0 -> get-trailhead-rating(map, vec2-create(x, y), height);
      _ -> 0;
    }
  })
}

assert-equal(solve-part-1(get-example()), 36);
assert-equal(solve-part-1(get-input()), 638);
assert-equal(solve-part-2(get-example()), 81);
assert-equal(solve-part-2(get-input()), 1289);
